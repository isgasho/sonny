digit -> [0,9]

letter -> [a,z] | [A,Z]

num -> digit num
	| digit

pitch_letter -> [A,G]

accidental -> # | b | \0

octave -> dig octave | digit | \0

real -> num
	| num .
	| num . num
	| . num
	| pi

note_notation -> pitch_letter accidental octave

pitch -> note_notation
	| real
	| _

dots -> . dots
	| \0

easy_duration -> w dots
	| h dots
	| q dots
	| e dots
	| s dots
	| ts dots

duration -> num / num
	| real

note -> pitch : duration

notes -> note , notes
	| \0

alpha_num_us -> letter | digit | _

id_chars -> alpha_num_us id_chars
	| alpha_num_us
	| \0

id -> letter id_chars
	| _ id_chars

period_ends -> duration
	| start
	| end
	| id.start
	| id.end
	| \0

period -> [ period_ends : period_ends ]
	| \0

built_in -> start | end | dur

global -> time

backlink -> ! num

term -> real
	| global
	| id
	| backlink
	| note
	| ( expression )

exp_un -> - term
	| sin term
	| cos term
	| floor term
	| ceil term
	| abs term
	| log term
	| term

exp_min_max -> exp_un min exp_min_max
	| exp_un max exp_min_max
	| exp_un

exp_pow -> exp_min_max ^ exp_pow
	| log exp_pow
	| exp_min_max

exp_mul -> exp_pow * exp_mul
	| exp_pow / exp_mul
	| exp_pow % exp_mul
	| exp_pow

exp_add -> exp_mul + exp_add
	| exp_mul - exp_add
	| exp_mul

expression -> exp_add

link -> expression
	| |chain_declaration|
	| { notes }

chain -> link -> chain
	| link

chain_declaration -> id period : chain
	| chain
